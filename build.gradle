buildscript {
  repositories {
    mavenCentral()
  }
}

apply plugin: 'idea'
apply plugin: 'distribution'

task wrapper(type: Wrapper) {
  gradleVersion = '3.4.1'
}

ext.src = 'src/main/mpack/'
ext.mpack = 'mpack.json'
ext.repostemplate = 'src/main/template/repos/repoinfo.xml'
ext.reposinfo554 = 'custom-services/SOLR/5.5.4/repos/'
ext.reposinfo661 = 'custom-services/SOLR/6.6.1/repos/'
ext.package661 = 'common-services/SOLR/6.6.1/package/'
ext.scripts661 = package661 + 'scripts/'
ext.alerts661 = package661 + 'alerts/'

distributions {
  main {
    contents {
      baseName = packageName
      from { src }

      from { src + mpack }
      filter { line ->
        line.replace("{VERSION}", "${version}")
      }

      into (scripts661) {
        from { src + scripts661 + 'params.py' }
        filter { line ->
          line.replace("{VERSION}", "${version}")
        }
      }

      into (alerts661) {
        from { src + alerts661 + 'alert_solr_cpu_metrics.py' }
        from { src + alerts661 + 'alert_solr_memory_metrics.py' }
        filter { line ->
          line.replace("{VERSION}", "${version}")
        }
      }

      into(reposinfo554) {
        from { repostemplate }
        filter { line ->
          line.replace("{REPOID}", "${repoid554}")
        }
      }

      into(reposinfo661) {
        from { repostemplate }
        filter { line ->
          line.replace("{REPOID}", "${repoid661}")
        }
      }
    }
  }
}

distTar {
  destinationDir = file(buildDir)
  appendix = 'mpack'
  compression = Compression.GZIP
  extension = 'tar.gz'
}


task makePackage(dependsOn: [clean, distTar]) {
}
